package com.kevin.drools.demo.basic.stateful;

rule "When there is a fire turn on the sprinkler"
when
    Fire($room : room)
    $sprinkler : Sprinkler(room == $room, on == false)
then
    modify($sprinkler) {setOn(true)};
    log.info("Turn on the sprinkler for room: [" + $room.getName() + ']');
end

rule "Raise the alarm when we have one or more fires"
when
    exists Fire()
then
    insert(new Alarm());
    log.info("Raise the alarm");
end

rule "When the fire is gone turn off the sprinkler"
when
    $room : Room( )
    $sprinkler : Sprinkler( room == $room, on == true )
    not Fire( room == $room )
then
    modify( $sprinkler ) { setOn( false ) };
    log.info( "Turn off the sprinkler for room: [" + $room.getName() + ']');
end

rule "Cancel the alarm when all the fires have gone"
when
    not Fire()
    $alarm : Alarm()
then
    delete($alarm);
    log.info("Cancel the alarm");
end

rule "Status output when things are ok"
when
    not Alarm()
    not Sprinkler(on == true)
then
    log.info("Everything is ok");
end